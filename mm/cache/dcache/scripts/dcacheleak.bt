#!/usr/bin/bpftrace --unsafe
/**
 * File: dcacheleak.bt 
 *
 * Description: Check dentry slab memory leak
 *
 * Author: Rong Tao <rongtao@cestc.cn>
 * Data: 2022-04-22
 *
 * How to test:
 * -------------------------------------------------
 * 1. Drop dcaches first, to make ALLOC & FREE even:
 *  $ while :; do sudo sh -c "echo 2 > /proc/sys/vm/drop_caches"; done
 *
 * 2. Execute dcacheleak.bt
 *  $ sudo ./dcacheleak.bt
 *
 * 3. Genrate negative dentry,
 *    It's should be "-bash: cd: /tmp/rongtaoj7p: No such file or directory"
 *  $ while :; do cd `mktemp -u /tmp/rongtaoXXX` 2>/dev/null; done
 *
 *
 */
#include <linux/slab.h>
#include <linux/dcache.h>

BEGIN
{
	printf("Tracing dcache leak. press ctrl-c to end.\n");
}

kprobe:__d_alloc
{
	@d_alloc_flag[pid] = 1;
}

/* Called in __d_alloc */
kprobe:kmem_cache_free
/@d_alloc_flag[pid] == 1/
{
	@dcount[" x ALLOC"] = count();
}

kretprobe:__d_alloc
{
	if (retval) {
		@dcount["   ALLOC"] = count();
	} else {
		@dcount[" x ALLOC"] = count();
	}
	@d_alloc_flag[pid] = 0;
}

kprobe:__d_free,
kprobe:__d_free_external
{
	@dcount["    FREE"] = count();
}

kprobe:d_lru_add,
kprobe:d_shrink_add
{
	@dcount[" LRU_ADD"] = count();
}

kprobe:d_lru_del,
kprobe:d_shrink_del
{
	@dcount[" LRU_DEL"] = count();
}

kprobe:__d_rehash
{
	@dcount["HASH_ADD"] = count();
}

/**
 * ERROR in Ubuntu 20.04.4 LTS
 * https://github.com/iovisor/bpftrace/issues/2199
 */
kprobe:__d_instantiate_anon
{
	$disconnected = arg2;

	if (!$disconnected) {
		@dcount["HASH_ADD"] = count();
	}
}

kprobe:___d_drop
{
	@dcount["HASH_DEL"] = count();
}

interval:s:1
{
	system("clear");
	system("echo /proc/sys/fs/dentry-state");
	system("cat /proc/sys/fs/dentry-state");
	print(@dcount);
}

END
{
	clear(@dcount);
	clear(@d_alloc_flag);
	printf("Bye.\n");
}
